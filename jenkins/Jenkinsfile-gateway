pipeline {
  agent none
  options {
    skipDefaultCheckout()
    skipStagesAfterUnstable()
  }
  environment {
    IMAGE_BASE = 'anshelen/microservices-gateway'
    IMAGE_TAG = "v$BUILD_NUMBER"
  	IMAGE_NAME = "${env.IMAGE_BASE}:${env.IMAGE_TAG}"
  	IMAGE_NAME_LATEST = "${env.IMAGE_BASE}:latest"
  	DOCKERFILE_NAME = "Dockerfile-packaged"
  }
  stages {
    stage('Checkout') {
      agent any
      steps {
        checkout scm
        stash(name: 'sources', includes: '**')
      }
    }

    stage("Prepare container") {
      agent {
        docker {
          image 'openjdk:11.0.5-slim'
          args '-v $HOME/.m2:/root/.m2'
        }
      }
      stages {
        stage('Build') {
          steps {
            unstash 'sources'
            sh './mvnw compile'
            stash(name: 'compiled', includes: '**')
          }
        }
        stage('Test') {
          steps {
            unstash 'compiled'
            sh './mvnw test'
            junit '**/target/surefire-reports/TEST-*.xml'
          }
        }
        stage('Package') {
          steps {
            unstash 'compiled'
            sh './mvnw package -DskipTests'
            stash(name: 'jar', includes: '**/target/microservices-gateway-*.jar')
            stash(name: 'dockerfile', includes: "${env.DOCKERFILE_NAME}")
          }
        }
      }
    }

    stage('Push images') {
      agent any
      when {
        branch 'master'
      }
      steps {
        unstash 'jar'
        unstash 'dockerfile'
        script {
          def imageName = "${env.IMAGE_NAME}"
          def dockerImage = docker.build("${env.IMAGE_NAME}", "-f ${env.DOCKERFILE_NAME} .")
          docker.withRegistry('', 'dockerhub-creds') {
            dockerImage.push()
            dockerImage.push("latest")
          }
          echo "Pushed Docker Image: ${env.IMAGE_NAME}"
        }
        sh "docker rmi ${env.IMAGE_NAME} ${env.IMAGE_NAME_LATEST}"
      }
    }

    stage('Trigger kubernetes') {
      agent any
      when {
        branch 'master'
      }
      steps {
        withKubeConfig([credentialsId: 'kubernetes-creds', serverUrl: "${CLUSTER_URL}", namespace: "${CLUSTER_NAMESPACE}"]) {
          sh "helm upgrade ${HELM_PROJECT} ${HELM_CHART} --reuse-values --set gateway.image.tag=${env.IMAGE_TAG}"
        }
      }
    }
  }
}
